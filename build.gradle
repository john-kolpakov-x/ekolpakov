plugins {
  id 'org.springframework.boot' version '2.1.8.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'maven'
  apply plugin: 'idea'

  repositories {
    mavenLocal()
    mavenCentral()
  }

  dependencies {
    ext.testNgVersion = '6.14.3'

    testCompile "org.testng:testng:$testNgVersion"
    testCompile 'org.assertj:assertj-core:3.10.0'
  }

  version = '0.0.1'

  sourceCompatibility = 11
  targetCompatibility = 11

  test.useTestNG()

  idea.module.inheritOutputDirs = false
  idea.module.outputDir = file("$buildDir/classes/main/")
  idea.module.testOutputDir = file("$buildDir/classes/test/")
  idea.module.downloadJavadoc = true
  idea.module.downloadSources = true

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  [compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

  sourceSets.main.java.srcDirs = ["src"]
  sourceSets.test.java.srcDirs = ["test_src"]
  sourceSets.main.resources.srcDirs = ["src_resources"]
  sourceSets.test.resources.srcDirs = ["test_resources"]

  sourceSets.main.resources {
    srcDirs += sourceSets.main.java.srcDirs
    exclude '**/*.java'
  }
  sourceSets.test.resources {
    srcDirs += sourceSets.test.java.srcDirs
    exclude '**/*.java'
  }

  jar {
    manifest {
      attributes 'Implementation-Title': project.name
      attributes 'Implementation-Version': version
      attributes 'Implementation-Vendor': "greetgo!"
      attributes 'Created-By': System.getProperty("user.name")
      attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
    }
  }

}
